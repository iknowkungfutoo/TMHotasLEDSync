/*-------------------------------------------------------------------------------
  --
  -- led_utils.tmh
  --
  -- Use at own risk without warranty.
  --
  -- Utility functions to program the state of LEDs on Thrustmaster Viper
  -- and Warthog devices.
  --
  -- Author: slughead
  -- Date: 23/03/2025
  --
  ------------------------------------------------------------------------------*/

include "viper_defines.tmh"
include "viper_utils.tmh"
include "warthog_defines.tmh"
include "warthog_utils.tmh"

define LED_STATE_OFF     0
define LED_STATE_ON      1
define LED_STATE_GREEN   1
define LED_STATE_RED     2
define LED_STATE_YELLOW  3
define LED_STATE_BLUE    4
define LED_STATE_CYAN    5
define LED_STATE_MAGENTA 6
define LED_STATE_WHITE   7
define LED_STATE_FLASH   8

define FLASH_INTERVAL 250

int char_to_int( char c )
{
    int result = 0;

    if (c >= '0' | c <= '9')
    {
        result = c - '0';
    }

    return c - '0';
}

int set_all_viper_leds( int led_state )
{
    int viper_device = get_viper_device();

    set_led(viper_device, LED_GEAR_NOSE,         led_state);
    set_led(viper_device, LED_GEAR_LEFT,         led_state);
    set_led(viper_device, LED_GEAR_RIGHT,        led_state);
    set_led(viper_device, LED_GEAR_WARNING,      led_state);
    set_led(viper_device, LED_RWR_SEARCH,        led_state);
    set_led(viper_device, LED_RWR_A_POWER,       led_state);
    set_led(viper_device, LED_RWR_LOW_ALT_GREEN, led_state);
    set_led(viper_device, LED_RWR_LOW_ALT_RED,   led_state);
    set_led(viper_device, LED_RWR_SYSTEM_POWER,  led_state);

    set_led(viper_device, LED_USER_LEFT_1,       led_state);
    set_led(viper_device, LED_USER_LEFT_2,       led_state);
    set_led(viper_device, LED_USER_LEFT_3,       led_state);
    set_led(viper_device, LED_USER_LEFT_4,       led_state);
    set_led(viper_device, LED_USER_LEFT_5,       led_state);

    set_led(viper_device, LED_USER_RIGHT_1,      led_state);
    set_led(viper_device, LED_USER_RIGHT_2,      led_state);
    set_led(viper_device, LED_USER_RIGHT_3,      led_state);
    set_led(viper_device, LED_USER_RIGHT_4,      led_state);
    set_led(viper_device, LED_USER_RIGHT_5,      led_state);
}

int set_all_warthog_leds( int led_state )
{
    set_led(&Throttle, get_tm_warthog_led_id( WH_LED1 ), led_state);
    set_led(&Throttle, get_tm_warthog_led_id( WH_LED2 ), led_state);
    set_led(&Throttle, get_tm_warthog_led_id( WH_LED3 ), led_state);
    set_led(&Throttle, get_tm_warthog_led_id( WH_LED4 ), led_state);
    set_led(&Throttle, get_tm_warthog_led_id( WH_LED5 ), led_state);
}

int reset_leds()
{
    // Viper Mission Pack or Viper Panel - assuming no one would have both!
    int viper_device = get_viper_device();
    int i = 0;

    if (viper_device != &joy0)
    {
        do
        {
            set_all_viper_leds(LED_STATE_OFF);
            set_all_viper_leds(LED_STATE_ON);
            set_all_viper_leds(LED_STATE_OFF);
            i=i+1;
        }
        while(i<3);
    }

    // Warthog Throttle
    if (&Throttle != &joy0)
    {
        i = 0;
        do
        {
            set_all_warthog_leds( LED_STATE_OFF );
            set_all_warthog_leds( LED_STATE_ON );
            set_all_warthog_leds( LED_STATE_OFF );
            i = i + 1;
        }
        while (i<3);

        set_backlight( 3 );
    }
}

int set_backlight( int intensity )
{
    ActKey(PULSE+KEYON+LED(&Throttle, LED_INTENSITY, intensity * 51));
}

int set_speed_brake_leds(int speed_brake_position)
{
    if ( aircraft == SU_25T |
         aircraft == SU_33 )
    {
        if (&Throttle != &joy0)
        {
            set_led(&Throttle, get_tm_warthog_led_id( WH_LED1 ), (speed_brake_position >= 1));
            set_led(&Throttle, get_tm_warthog_led_id( WH_LED2 ), (speed_brake_position >= 2));
            set_led(&Throttle, get_tm_warthog_led_id( WH_LED3 ), (speed_brake_position >= 3));
            set_led(&Throttle, get_tm_warthog_led_id( WH_LED4 ), (speed_brake_position >= 4));
            set_led(&Throttle, get_tm_warthog_led_id( WH_LED5 ), (speed_brake_position >= 5));
        }
    }
}

// TODO led flashing currently does not support individual flashing LEDs
// and is only supported on ViperTQS/ViperBBox for Search indicator.
int flash_led = 0;

int set_led(int device, int led_id, int led_state)
{
    int i;

    if (device == &ViperTQS | device == &ViperBBox)
    {
        if (led_state == LED_STATE_FLASH)
        {
            if (led_id == LED_RWR_SEARCH & flash_led == 0)
            {
                flash_led = 1;
                start_led_flash_event_loop( device );
            }
        }
        else
        {
            if (led_id == LED_RWR_SEARCH & flash_led == 1)
            {
                flash_led = 0;
            }

            // turn led on or off
            // LEDV commands need to be sent multiple times to be certain they are executed on the device
            i = 0; while (i<3)
            {
                ActKey(PULSE+KEYON+LEDV( device, led_id, led_state ));
                i = i+1;
            }
        }
    }
    else if (device == &Throttle)
    {
        if (led_state == LED_STATE_FLASH)
        {
            if (led_id == get_tm_warthog_led_id( WH_LED1 ) & flash_led == 0)
            {
                flash_led = 1;

                start_led_flash_event_loop( device );
            }
        }
        else
        {
            if (led_id == get_tm_warthog_led_id( WH_LED1 ) & flash_led == 1)
            {
                flash_led = 0;
            }

            // turn led on or off
            if (led_state == LED_STATE_OFF)
            {
                ActKey(PULSE+KEYON+LED(device, LED_ONOFF, LED_CURRENT-led_id));
            }
            else
            {
                ActKey(PULSE+KEYON+LED(device, LED_ONOFF, LED_CURRENT+led_id));
            }
        }
    }
}

int led_next_state;
int flash_device;

int start_led_flash_event_loop( int device )
{
    // FIRST CALL .. ON SOME EVENT
    flash_device = device;
    led_next_state = LED_STATE_OFF;
    handle_flash_cycle(0);
}

int handle_flash_cycle(int cycle_num)
{
    int i;

    if (flash_led == 1)
    {
        if (flash_device == &ViperTQS | flash_device == &ViperBBox)
        {
            // LEDV commands need to be sent multiple times to be certain they are executed on the device
            i = 0; while (i<3)
            {
                ActKey(PULSE+KEYON+LEDV( flash_device, LED_RWR_SEARCH, led_next_state ));
                i = i+1;
            }

            // set next state of flashing led
            if ( led_next_state == LED_STATE_ON)
            {
                led_next_state = LED_STATE_OFF;
            }
            else
            {
                led_next_state = LED_STATE_ON;
            }

            // NEXT CYCLE LOOP
            DeferCall(FLASH_INTERVAL, &handle_flash_cycle, cycle_num + 1);
        }
        else if (flash_device == &Throttle)
        {
            if (led_next_state == LED_STATE_ON)
            {
                ActKey(PULSE+KEYON+LED(flash_device, LED_ONOFF, LED_CURRENT + get_tm_warthog_led_id( WH_LED1 )));
            }
            else
            {
                ActKey(PULSE+KEYON+LED(flash_device, LED_ONOFF, LED_CURRENT - get_tm_warthog_led_id( WH_LED1 )));
            }

            // set next state of flashing led
            if ( led_next_state == LED_STATE_ON)
            {
                led_next_state = LED_STATE_OFF;
            }
            else
            {
                led_next_state = LED_STATE_ON;
            }

            // NEXT CYCLE LOOP
            DeferCall(FLASH_INTERVAL, &handle_flash_cycle, cycle_num + 1);
        }
    }
}
